apply plugin: 'com.android.application'
apply plugin: 'com.getkeepsafe.dexcount'
apply plugin: 'com.neenbedankt.android-apt'

task wrapper(type: Wrapper) {
    gradleVersion = '2.14'
}

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:2.1.2'
        classpath 'com.getkeepsafe.dexcount:dexcount-gradle-plugin:0.5.2'
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.8'
    }
}

repositories {
    jcenter()
}

android {
    lintOptions {
        lintConfig file("lint.xml")
    }

    compileSdkVersion 24
    buildToolsVersion "24.0.1"

    defaultConfig {
        applicationId "org.tasks"
        versionCode 431
        versionName "4.9.3"
        minSdkVersion 15
        targetSdkVersion 24
        multiDexEnabled true
        testInstrumentationRunner "com.android.test.runner.MultiDexTestRunner"
//        jackOptions {
//            enabled true
//        }
    }

    signingConfigs {
        release
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
//        sourceCompatibility JavaVersion.VERSION_1_8
//        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        debug {
            testCoverageEnabled true
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard.pro'
            signingConfig signingConfigs.release
        }
    }

    if (project.hasProperty('keyAlias') &&
            project.hasProperty('storeFile') &&
            project.hasProperty('storePassword') &&
            project.hasProperty('keyPassword')) {
        android.signingConfigs.release.keyAlias = keyAlias
        android.signingConfigs.release.storeFile = file(storeFile)
        android.signingConfigs.release.storePassword = storePassword
        android.signingConfigs.release.keyPassword = keyPassword
    } else {
        buildTypes.release.signingConfig = null
    }

    flavorDimensions 'store', 'env'

    productFlavors {
        generic {
            dimension 'store'
        }
        googleplay {
            dimension 'store'
        }
        amazon {
            dimension 'store'
        }
        dev {
            dimension 'env'
            minSdkVersion 21
        }
        prod {
            dimension 'env'
        }
    }
}

configurations {
    all*.exclude group: 'com.google.guava', module: 'guava-jdk5'
    all*.exclude group: 'org.apache.httpcomponents', module: 'httpclient'
}

final DAGGER_VERSION = '2.5'
final BUTTERKNIFE_VERSION = '8.2.1'
final GPS_VERSION = '9.4.0'
final SUPPORT_VERSION = '24.1.1'
final STETHO_VERSION = '1.3.1'

dependencies {
    apt "com.google.dagger:dagger-compiler:${DAGGER_VERSION}"
    compile "com.google.dagger:dagger:${DAGGER_VERSION}"

    apt "com.jakewharton:butterknife-compiler:${BUTTERKNIFE_VERSION}"
    compile "com.jakewharton:butterknife:${BUTTERKNIFE_VERSION}"

    debugCompile "com.facebook.stetho:stetho:${STETHO_VERSION}"
    debugCompile "com.facebook.stetho:stetho-timber:${STETHO_VERSION}@aar"
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.4-beta2'

    compile 'com.github.rey5137:material:1.2.4'
    compile 'com.android.support:multidex:1.0.1'
    compile 'com.nononsenseapps:filepicker:2.5.2'
    compile "com.android.support:design:${SUPPORT_VERSION}"
    compile 'com.jakewharton.timber:timber:4.1.2'
    compile 'com.google.guava:guava:19.0'
    compile 'com.jakewharton:process-phoenix:1.0.2'
    compile ('com.rubiconproject.oss:jchronic:0.2.6') {
        transitive = false
    }
    compile ('org.scala-saddle:google-rfc-2445:20110304') {
        transitive = false
    }
    compile ('com.wdullaer:materialdatetimepicker:2.3.0') {
        exclude group: 'com.android.support', module: 'support-v4'
    }

    googleplayCompile 'com.google.android.apps.dashclock:dashclock-api:2.0.0'
    googleplayCompile 'com.twofortyfouram:android-plugin-api-for-locale:[1.0.1,2.0['
    googleplayCompile "com.google.android.gms:play-services-location:${GPS_VERSION}"
    googleplayCompile "com.google.android.gms:play-services-analytics:${GPS_VERSION}"
    googleplayCompile "com.google.android.gms:play-services-auth:${GPS_VERSION}"
    googleplayCompile "com.google.android.gms:play-services-places:${GPS_VERSION}"
    googleplayCompile 'com.google.apis:google-api-services-tasks:v1-rev45-1.22.0'
    googleplayCompile 'com.google.api-client:google-api-client-android:1.22.0'

    amazonCompile "com.google.android.gms:play-services-analytics:${GPS_VERSION}"

    androidTestApt "com.google.dagger:dagger-compiler:${DAGGER_VERSION}"
    androidTestApt "com.jakewharton:butterknife-compiler:${BUTTERKNIFE_VERSION}"
    androidTestCompile 'com.google.dexmaker:dexmaker-mockito:1.2'
    androidTestCompile 'com.natpryce:make-it-easy:4.0.0'
}
